#! zsh
zle-line-init() {}
zle -N zle-line-init

autoload -Uz select-word-style
select-word-style shell

# Allow S-Tab to backtrack through the completion menu.
bindkey $terminfo[kcbt] reverse-menu-complete

# Tweaks to vi mode:
bindkey '^?' backward-delete-char # backspace over the start of Insert mode
bindkey 'jj' vi-cmd-mode # inoremap jj <Esc>

# Backward-kill long paths one directory at a time.
bindkey '^W' backward-kill-word
zstyle ':zle:backward-kill-word' word-style unspecified
zstyle ':zle:backward-kill-word' word-chars ' /'

# Make ^Z resume your suspended job if there is one.
autoload fancy-ctrl-z
zle -N fancy-ctrl-z
bindkey '^Z' fancy-ctrl-z

# Allow for magic such as url-quote-magic to apply when your terminal supports
# bracketed paste.
autoload -Uz bracketed-paste-magic 
zle -N bracketed-paste bracketed-paste-magic

# url-quote-magic will automatically quote special chars in URLs pasted into
# the terminal.
autoload -Uz url-quote-magic
url-quote-magic-with-highlight() { url-quote-magic; _zsh_highlight }
zle -N self-insert url-quote-magic-with-highlight

__fzf-z() {
	z -l | tac | cut -c 12- | fzf --preview 'tree -FCL 2 {}'
}
fzf-z() {
	LBUFFER+="$(__fzf-z)"
	zle redisplay
}
zle -N fzf-z
bindkey '^G' fzf-z
